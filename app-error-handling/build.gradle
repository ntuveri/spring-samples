plugins {
	id 'org.springframework.boot' version '2.2.5.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
    id "com.github.spotbugs" version "4.0.8"
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id "de.aaschmid.cpd" version "3.1"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

cpdCheck {
    ignoreFailures true
    reports {
        xml.enabled = true
        xml.destination file("${buildDir}/reports/cpd/cpd.xml")
    }
    source = sourceSets.main.allJava
}

pmd {
    ruleSetFiles = files("config/pmd/ruleset.xml")
    ignoreFailures = true
    sourceSets = [sourceSets.main]
    rulePriority(2)
}

pmdMain {
    reports {
        xml.enabled true
        html.enabled false
        xml.destination file("${buildDir}/reports/pmd/pmd.xml")
    }
}

pmdTest {
    reports {
        xml.enabled true
        html.enabled false
        xml.destination file("${buildDir}/reports/pmd/pmdTest.xml")
    }
}

spotbugs {
    effort = "max"
    reportLevel = "low"
    ignoreFailures = true
}

spotbugsMain {
    reports {
        html {
            enabled = false
        }
        xml {
            enabled = true
            destination = file("${buildDir}/reports/spotbugs/spotbugsMain.xml")
        }
    }
}

spotbugsTest {
    reports {
        html.enabled = false
        xml.enabled = true
        xml.destination = file("${buildDir}/reports/spotbugs/spotbugsTest.xml")
    }
}

checkstyle {
    toolVersion '8.30'
    ignoreFailures true
    configFile file("${rootDir}/config/checkstyle/checkstyle.xml")
}

checkstyleMain {
    source = 'src/main/java'
    reports {
        html.enabled true
        html.destination file("${buildDir}/reports/checkstyle/checkstyleMain.html")
        xml.enabled true
        xml.destination file("${buildDir}/reports/checkstyle/checkstyleMain.xml")
    }
}

checkstyleTest {
    source = 'src/test/java'
    reports {
        html.enabled true
        html.destination file("${buildDir}/reports/checkstyle/checkstyleTest.html")
        xml.enabled true
        xml.destination file("${buildDir}/reports/checkstyle/checkstyleTest.xml")
    }
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

ext {
	set('keycloakVersion', '5.0.0')
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.keycloak:keycloak-spring-boot-starter'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

	testImplementation('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
	imports {
		mavenBom "org.keycloak.bom:keycloak-adapter-bom:${keycloakVersion}"
	}
}
